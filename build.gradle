plugins {
    id 'org.asciidoctor.jvm.convert' version '4.0.2'
    id 'org.asciidoctor.jvm.pdf' version '4.0.2'
}

defaultTasks 'clean', 'asciidoctor'

repositories {
    mavenCentral()
}

asciidoctorj {
    version = '2.5.9'
    modules {
        pdf {
            version '2.3.9'
        }
    }
    docExtensions {
        // Preprocessor for since/until roles in pdf backend (in html5 backed this is handled using CSS)
        preprocessor {
            document, reader ->
            if (document.options['backend'] != 'pdf') {
                println 'skipping since/until preprocessor for non-PDF'
                return
            }
            def pattern = ~/\[\.(since|until)\]_([^_]+?)_/
            def allLines = reader.readLines()
            def replacement = allLines.collect { line ->
                def matcher = pattern.matcher(line)
                if (matcher.find()) {
                    def buffer = new StringBuffer(line.length() + 16)
                    while (true) {
                        def replacement = matcher.group(1) == "since" ? /[.since]_**Since:** $2_/ : /[.until]_**Removed in:** $2_/
                        matcher.appendReplacement(buffer, replacement)
                        if (!matcher.find()) break;
                    }
                    matcher.appendTail(buffer)
                    return buffer.toString()
                } else {
                    return line
                }
            }
            reader.restoreLines(replacement)
        }
    }
}

asciidoctor {
    baseDirFollowsSourceDir()
    executionMode = OUT_OF_PROCESS
    sources {
        include 'jaybird_manual.adoc'
    }
    attributes 'revnumber': false, 'stylesdir': file('src/docs/theme/jaybird-html'), 'stylesheet': 'firebird.css',
            'docinfo': 'shared', 'docinfodir': file('src/docs/theme/jaybird-html/docinfo')
    jvm {
        jvmArgs "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED"
    }
}

pdfThemes {
    local 'jaybird', {
        themeDir = file('src/docs/theme/jaybird-pdf')
        themeName = 'jaybird'
    }
}

asciidoctorPdf {
    baseDirFollowsSourceDir()
    executionMode = OUT_OF_PROCESS
    sources {
        include 'jaybird_manual.adoc'
    }
    attributes 'revnumber': false, 'source-highlighter': 'rouge', 'media': 'prepress', 'compress': '',
            'icon-set': 'fas', 'pdf-fontsdir': "${file('src/docs/theme/fonts')},GEM_FONTS_DIR"
    theme 'jaybird'
    jvm {
        jvmArgs "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED"
    }
}

// Create the HTML and PDF output and copy them to the build/pages folder
// Primarily intended for the GitHub Actions build to deploy to GitHub pages
tasks.register('makePages') {
    dependsOn asciidoctor, asciidoctorPdf
    doLast {
        copy {
            from asciidoctor.outputs
            into "$buildDir/pages"
        }
        copy {
            from asciidoctorPdf.outputs
            into "$buildDir/pages"
        }
    }
}

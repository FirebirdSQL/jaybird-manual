plugins {
    id 'org.asciidoctor.jvm.convert' version '4.0.4'
    id 'org.asciidoctor.jvm.pdf' version '4.0.4'
}

defaultTasks 'clean', 'asciidoctor'

repositories {
    mavenCentral()
}

asciidoctorj {
    // asciidoctorj 3.0.0 doesn't seem to work with asciidoctor-gradle-plugin 4.0.3
    version = '2.5.13'
    modules {
        pdf {
            version '2.3.18'
        }
    }
    docExtensions file('doc-extension.groovy')
}

asciidoctor {
    baseDirFollowsSourceDir()
    executionMode = OUT_OF_PROCESS
    sources {
        include 'jaybird_manual.adoc'
    }
    attributes 'revnumber': false, 'stylesdir': file('src/docs/theme/jaybird-html'), 'stylesheet': 'firebird.css',
            'docinfo': 'shared', 'docinfodir': file('src/docs/theme/jaybird-html/docinfo')
    jvm {
        jvmArgs "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED"
    }
}

pdfThemes {
    local 'jaybird', {
        themeDir = file('src/docs/theme/jaybird-pdf')
        themeName = 'jaybird'
    }
}

asciidoctorPdf {
    baseDirFollowsSourceDir()
    executionMode = OUT_OF_PROCESS
    sources {
        include 'jaybird_manual.adoc'
    }
    attributes 'revnumber': false, 'source-highlighter': 'rouge', 'media': 'prepress', 'compress': '',
            'icon-set': 'fas', 'pdf-fontsdir': "${file('src/docs/theme/fonts')},GEM_FONTS_DIR"
    theme = 'jaybird'
    jvm {
        jvmArgs "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED"
    }
}

// Create the HTML and PDF output and copy them to the build/pages folder
// Primarily intended for the GitHub Actions build to deploy to GitHub pages
tasks.register('makePages') {
    dependsOn asciidoctor, asciidoctorPdf
    doLast {
        copy {
            from asciidoctor.outputs
            into layout.buildDirectory.dir('pages')
        }
        copy {
            from asciidoctorPdf.outputs
            into layout.buildDirectory.dir('pages')
        }
    }
}
